#!/usr/bin/perl
#
# Check Perl scripts for strict, warnings, and syntax.
#
# Checks all Perl scripts in the tree for problems uncovered by Test::Strict.
# This includes using strict and warnings for every script and ensuring they
# all pass a syntax check.  Currently, test suite coverage is not checked.
# This test is disabled unless RRA_MAINTAINER_TESTS is set, since some scripts
# may require dependencies that aren't necessarily present.
#
# This test currently does not arrange to add any in-tree Perl modules to the
# library search path.
#
# The canonical version of this file is maintained in the rra-c-util package,
# which can be found at <http://www.eyrie.org/~eagle/software/rra-c-util/>.
#
# Written by Russ Allbery <rra@stanford.edu>
# Copyright 2012, 2013
#     The Board of Trustees of the Leland Stanford Junior University
#
# Permission is hereby granted, free of charge, to any person obtaining a
# copy of this software and associated documentation files (the "Software"),
# to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense,
# and/or sell copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
# DEALINGS IN THE SOFTWARE.

use 5.006;
use strict;
use warnings;

use File::Spec;
use Test::More;

# Prefer the modules built as part of our source package.  Otherwise, we may
# not find Perl modules while testing, or find the wrong versions.
use lib "$ENV{BUILD}/../perl/blib/arch";
use lib "$ENV{BUILD}/../perl/blib/lib";

# Allow the WebAuth module to find the libraries built in the source tree.
if ($ENV{LD_LIBRARY_PATH}) {
    $ENV{LD_LIBRARY_PATH} .= ":$ENV{BUILD}/../lib/.libs";
} else {
    $ENV{LD_LIBRARY_PATH} = "$ENV{BUILD}/../lib/.libs";
}

# Top-level directories to ignore.  We ignore the perl directory if it exists
# since, in my packages, it is treated as a Perl module distribution and has
# its own standalone test suite.
my %IGNORE = map { $_ => 1 } qw(.git perl);

# Bail if SOURCE is not set.
if (!$ENV{SOURCE}) {
    BAIL_OUT('SOURCE not defined (run under runtests)');
}

# Skip tests unless we're running the test suite in maintainer mode.  Random
# scripts in the source directory may require optional Perl modules be
# installed or have other dependencies that can't be satisfied by every user.
if (!$ENV{RRA_MAINTAINER_TESTS}) {
    plan skip_all => 'Perl syntax tests only run for maintainer';
}

# Skip tests if Test::Strict is not installed.
if (!eval { require Test::Strict }) {
    plan skip_all => 'Test::Strict required to test Perl scripts';
    $Test::Strict::TEST_WARNINGS = 0;    # suppress warning
}
Test::Strict->import;

# SOURCE will be the test directory.  Change to the parent directory.
my ($rootvol, $rootdirs) = File::Spec->splitpath($ENV{SOURCE}, 1);
my @rootdirs = File::Spec->splitdir($rootdirs);
pop @rootdirs;
if ($rootdirs[-1] eq File::Spec->updir) {
    pop @rootdirs;
    pop @rootdirs;
}
my $root = File::Spec->catpath($rootvol, File::Spec->catdir(@rootdirs), q{});
chdir $root or BAIL_OUT("cannot chdir to $root: $!");

# If BUILD is a subdirectory of SOURCE, ignore it as well.
my ($buildvol, $builddirs) = File::Spec->splitpath($ENV{BUILD}, 1);
my @builddirs = File::Spec->splitdir($builddirs);
pop @builddirs;
if ($buildvol eq $rootvol && @builddirs == @rootdirs + 1) {
    while (@rootdirs && $builddirs[0] eq $rootdirs[0]) {
        shift @builddirs;
        shift @rootdirs;
    }
    if (@builddirs == 1) {
        $IGNORE{ $builddirs[0] } = 1;
    }
}

# Build the list of directories to test.
opendir my $rootdir, q{.} or BAIL_OUT("cannot open .: $!");
my @dirs = grep { -d $_ && !$IGNORE{$_} } readdir $rootdir;
closedir $rootdir;
@dirs = File::Spec->no_upwards(@dirs);

# Finally, run the actual tests.  We also want to check use warnings.
$Test::Strict::TEST_WARNINGS = 1;
all_perl_files_ok(@dirs);
