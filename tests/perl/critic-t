#!/usr/bin/perl
#
# Check for perlcritic errors in included Perl scripts.
#
# Checks all Perl scripts in the tree for problems uncovered by perlcritic.
# This test is disabled unless RRA_MAINTAINER_TESTS is set, since coding style
# will not interfere with functionality.
#
# Copyright 2012
#     The Board of Trustees of the Leland Stanford Junior University
#
# See LICENSE for licensing terms.

use strict;
use warnings;

use File::Spec;
use Test::More;

# Files to test.
my @FILES = qw(
  lib/encoding-rules
  tests/perl/critic-t
  tests/perl/strict-t
  tools/webauth-make-tokens
);

# Bail if SOURCE is not set, and determine the directory that all further
# file names will be relative to.
if (!$ENV{SOURCE}) {
    BAIL_OUT('SOURCE not defined (run under runtests)');
}
my ($volume, $dirs) = File::Spec->splitpath($ENV{SOURCE}, 1);
my @dirs = File::Spec->splitdir($dirs);
pop @dirs;
my $BASE = File::Spec->catpath($volume, File::Spec->catdir(@dirs));

# Skip tests unless we're running the test suite in maintainer mode.
if (!$ENV{RRA_MAINTAINER_TESTS}) {
    plan skip_all => 'Coding style tests only run for maintainer';
}

# Skip tests if Test::Perl::Critic is not installed.
if (!eval { require Test::Perl::Critic }) {
    plan skip_all => 'Test::Perl::Critic required to test coding style';
}

# Build the list of files to test.
my @paths = map { File::Spec->catfile($BASE, $_) } @FILES;

# Declare a plan, now that we know the list of files to check.
plan tests => scalar @paths;

# Force the embedded Perl::Tidy check to use the correct configuration.
local $ENV{PERLTIDY} = $ENV{SOURCE} . '/data/perltidyrc';

# Import the configuration file and run Perl::Critic.
my $profile = "$ENV{SOURCE}/data/perlcriticrc";
Test::Perl::Critic->import(-profile => $profile);
for my $path (@paths) {
    critic_ok($path);
}
